apply plugin: 'com.android.application'

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = code
        }
        return code.toString().toInteger()
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        String result = stdout.toString().trim()
        return result.substring(0, result.indexOf('-')) + "." +
                result.substring(result.indexOf('-') + 1, result.lastIndexOf('-'))
    }
    catch (ignored) {
        return null;
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    flavorDimensions "default"

    defaultConfig {
        applicationId "tim.android"
        minSdkVersion 28
        targetSdkVersion 28
        versionCode 28 + getVersionCode()
        versionName getVersionName()
        setProperty("archivesBaseName", "$applicationId-$versionName")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            dimension "default"
            manifestPlaceholders = [ROTATION_PREF: "unspecified"]
            versionNameSuffix = "-dev"
        }

        stable {
            dimension "default"
            manifestPlaceholders = [ROTATION_PREF: "portrait"]
        }
    }
    packagingOptions {
        exclude 'plugin.properties'
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.core:core:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0-alpha07'
    implementation 'android.arch.navigation:navigation-ui:1.0.0-alpha07'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.0.0'

    implementation "android.arch.persistence.room:runtime:2.1.0-alpha02"
    annotationProcessor "android.arch.persistence.room:compiler:2.1.0-alpha02"

    implementation "android.arch.lifecycle:extensions:2.0.0"
    annotationProcessor "android.arch.lifecycle:compiler:2.0.0"

    implementation 'com.github.esafirm.android-image-picker:imagepicker:1.13.1'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    implementation 'com.prof.rssparser:rssparser:1.4.4'
    implementation 'com.google.code.gson:gson:2.8.5'
}
